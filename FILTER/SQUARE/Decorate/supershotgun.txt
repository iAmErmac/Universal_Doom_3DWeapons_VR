ACTOR Vanilla_SuperShotgun : SuperShotgun
{
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoUse2 1
	States
	{
	Deselect:
		TNT1 A 0 A_JumpIf(GetCVAR("evw_fastswitch")==0, 2)
		SHT2 A 1 A_Lower(60)
		Loop
		SHT2 A 1 A_Lower
		Loop
	Select:
		TNT1 A 0 A_JumpIf(GetCVAR("evw_fastswitch")==0, 2)
		SHT2 A 1 A_Raise(60)
		Loop
		SHT2 A 1 A_Raise
		Loop
	Fire:
		SHT2 A 3
		TNT1 A 0 A_JumpIf(GetCVAR("evw_weapons")==1, "Fire.Enhanced")
		SHT2 A 0 A_FireBullets (11.2, 7.1, 20, 5, "BulletPuff")
		SHT2 A 0 A_PlaySound ("weapons/sshotf", CHAN_WEAPON)
		SHT2 A 7 A_GunFlash
		SHT2 B 7
		SHT2 C 7 A_CheckReload
		SHT2 D 7 A_PlaySound ("weapons/sshoto", CHAN_WEAPON)
		SHT2 E 7
		SHT2 F 7 A_PlaySound ("weapons/sshotl", CHAN_WEAPON)
		SHT2 G 6
		SHT2 H 0 A_PlaySound ("weapons/sshotc", CHAN_WEAPON)
		SHT2 H 6 A_ReFire
		SHT2 A 5 A_ReFire
		Goto Ready
	Fire.Enhanced:
		TNT1 A 0 A_JumpIfInventory("SSGshotfired",1,"AltFireSecond")
		SHT2 A 0 bright A_FireBullets (11.2, 7.1, 20, 5, "EnShotPuff")
		SHT2 A 0 A_PlaySound ("weapons/ssg/fire", CHAN_WEAPON)
		SHT2 A 7 A_GunFlash
		//TNT1 A 0 A_SpawnItemEx("WeaponSmoke",cos(pitch)*25,random(1,2),sin(-pitch)*25+random(34,36),0,0,1,0)
		TNT1 A 0 A_FireProjectile("ShotgunBarrelSmoke",0,0,0,0,FPF_NOAUTOAIM,0)
		SHT2 B 7
		SHT2 C 7 A_CheckReload
		SHT2 D 7 A_PlaySound ("weapons/ssg/open", CHAN_WEAPON)
		SHT2 E 7
		SHT2 F 7 A_PlaySound ("weapons/ssg/in", CHAN_WEAPON)
		SHT2 G 6
		SHT2 H 0 A_PlaySound ("weapons/ssg/close", CHAN_WEAPON)
		SHT2 H 6 A_ReFire
		SHT2 A 5 A_ReFire
		//TNT1 A 0 A_SpawnItemEx("WeaponSmoke",cos(pitch)*25,random(1,2),sin(-pitch)*25+random(34,36),0,0,1,0)
		//TNT1 A 0 A_SpawnItemEx("WeaponSmoke",cos(pitch)*25,random(-1,-2),sin(-pitch)*25+random(34,36),0,0,1,0)
		TNT1 A 0 A_FireProjectile("ShotgunBarrel1Smoke",0,0,0,0,FPF_NOAUTOAIM,0)
		TNT1 A 0 A_FireProjectile("ShotgunBarrel2Smoke",0,0,0,0,FPF_NOAUTOAIM,0)
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIf(GetCVAR("evw_weapons")==0, "Ready") //Alt fire is available on enhanced mode only
		TNT1 A 0 A_JumpIfInventory("SSGshotfired",1,"AltFireSecond")
		SHT2 A 3
		TNT1 A 0 A_GiveInventory("SSGshotFired",1)
		SHT2 A 0 bright A_FireBullets(10,6.5,10,5,"EnShotPuff")
		SHT2 A 0 A_PlaySound ("weapons/ssg/fire", CHAN_WEAPON)
		SHT2 A 1 A_GunFlash("AltFlash")
		//TNT1 A 0 A_SpawnItemEx("WeaponSmoke",cos(pitch)*25,random(1,2),sin(-pitch)*25+random(34,36),0,0,1,0)
		TNT1 A 0 A_FireProjectile("ShotgunBarrel1Smoke",0,0,0,0,FPF_NOAUTOAIM,0)
		SHT2 BC 3
		goto ready
	AltFireSecond:
		SHT2 A 3
		TNT1 A 0 A_Takeinventory("SSGshotFired",1)
		SHT2 A 0 bright A_FireBullets(10,6.5,10,5,"EnShotPuff")
		SHT2 A 0 A_PlaySound ("weapons/ssg/fire", CHAN_WEAPON)
		SHT2 A 1 A_GunFlash("AltFlash")
		//TNT1 A 0 A_SpawnItemEx("WeaponSmoke",cos(pitch)*25,random(1,2),sin(-pitch)*25+random(34,36),0,0,1,0)
		TNT1 A 0 A_FireProjectile("ShotgunBarrel2Smoke",0,0,0,0,FPF_NOAUTOAIM,0)
		SHT2 B 5
		SHT2 C 5 A_CheckReload
		SHT2 D 7 A_PlaySound ("weapons/ssg/open", CHAN_WEAPON)
		SHT2 E 7
		SHT2 F 7 A_PlaySound ("weapons/ssg/in", CHAN_WEAPON)
		SHT2 G 6
		SHT2 H 0 A_PlaySound ("weapons/ssg/close", CHAN_WEAPON)
		SHT2 H 6 A_ReFire
		SHT2 A 5 A_ReFire
		goto ready
	Flash:
		TNT1 A 0 A_JumpIf(GetCVAR("evw_tracers")==false, "Flash.Vanilla")
		TNT1 AAAAAA 0 A_FireProjectile("BulletTracer",random(-3,3),0,0,0,FPF_NOAUTOAIM,random(-1,1))
	Flash.Vanilla:
		SHT2 I 4 Bright A_Light1
		SHT2 J 3 Bright A_Light2
		Goto LightDone
	AltFlash:
		TNT1 A 0 A_JumpIf(GetCVAR("evw_tracers")==false, "Flash.Vanilla")
		TNT1 AAA 0 A_FireProjectile("BulletTracer",random(-6,6),0,0,0,FPF_NOAUTOAIM,random(-3,3))
	AltFlash1:
		TNT1 A 0 A_JumpIfInventory("SSGshotfired",1,"AltFlash2")
		SHTF A 4 Bright A_Light1
		Goto LightDone
	AltFlash2:
		SHTF B 4 Bright A_Light1
		Goto LightDone
	}
}

//Used as flag for enhanced mode alt fire only
Actor SSGshotFired : DummyChecker
{
	inventory.maxamount 1
}