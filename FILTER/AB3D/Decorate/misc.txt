//SFX codes from Brutal Doom

Actor ExplosionParticle
{
  Height 1
  Radius 1
  Mass 0
  +Missile
  +NoBlockMap
  +DontSplash
  +FORCEXYBILLBOARD
  +CLIENTSIDEONLY
  +THRUACTORS
  +GHOST
  -NOGRAVITY
  +THRUGHOST
  +NOTELEPORT
  RenderStyle Add
  Scale 0.8
  Gravity 0
  States
  {
  Spawn:
    SPKO B 1 Bright A_FadeOut(0.02)
    Loop
  }
}

Actor ExplosionParticle2: ExplosionParticle
{

  Scale 0.1
  States
  {
  Spawn:
    SPRK SSSSSSSS 1 BRIGHT
	SPRK SSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSS 1 Bright A_FadeOut(0.02)
    Stop
  }
}

ACTOR ExplosionParticleHeavy: ExplosionParticle2
{
	speed 5
	Gravity 0.5
	Scale 0.2
	BounceFactor 0.01
    States
    {
	Spawn:
		SPRK SSSSS 1 BRIGHT
		SPRK SSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSS 1 Bright A_FadeOut(0.02)
		stop
	Death:
		Stop
	}
}

ACTOR ShotgunParticles: ExplosionParticle2
{
 speed 15
 radius 8
 height 1
 Gravity 0.6
 RenderStyle Add
 Scale 0.1
 Alpha 0.9
    States
    {
     Spawn:
	  TNT1 A 2
       SPKO BBBB 1 Bright A_FadeOut(0.02)
	   TNT1 A 0 A_ChangeFlag(NOGRAVITY, 0)
	   SPKO BBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBB 1 Bright A_FadeOut(0.04)
        stop
     Death:
       Stop
    }
}

ACTOR ShotgunParticles2 : ShotgunParticles
{
  speed 10
  Gravity 0.5
  Scale 0.1
  Alpha 0.9
}

ACTOR ShotgunParticlesHeavy : ExplosionParticleHeavy
{
	speed 3
	Scale 0.1
}


ACTOR HitSpark: ExplosionParticleHeavy
{
 speed 6
  Scale 0.04
 Gravity 1.0
   +NOCLIP
    States
    {
     Spawn:
       SPKO SSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSS 1 Bright A_FadeOut(0.07)
        stop
     Death:
       Stop
    }
}

Actor ExplosionParticleSpawner
{
  +NOCLIP
  +NOBLOCKMAP
  +CLIENTSIDEONLY
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 AAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 360), 2, random (0, 180))
    TNT1 AAAAA 0 A_CustomMissile ("ExplosionParticleHeavy", 0, 0, random (0, 180), 2, random (0, 180))
    TNT1 A 1
    Stop
  }
}

ACTOR SparkX: ExplosionParticleHeavy
{
	speed 10
	Gravity 0.8
	-NOGRAVITY
	Scale 0.05
	radius 1
	height 1
	+DOOMBOUNCE
	+GHOST
	BounceFactor 0.5
	Damage 0
	alpha 1.0
    States
    {
	Spawn:
		SPKO AAA 1 BRIGHT
		SPKO AAAAAAA 1 Bright A_FadeOut(0.20)
		stop
	Death:
		TNT1 A 0
		Stop
    }
}

ACTOR SparkXNoModel: SparkX
{
	Scale 0.03
	Gravity 0.7
}

ACTOR SparkXHeavy: SparkX
{
	speed 3
	Gravity 0.5
	BounceFactor 0.01
	Scale 0.1
	States
    {
	Spawn:
		SPRK SSSSS 1 BRIGHT
		SPRK SSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSS 1 Bright A_FadeOut(0.02)
		stop
	Death:
		Stop
    }
}



//SFX codes from Brutal/Beautiful Doom

Actor DebrisGeneral
{
+FIXMAPTHINGPOS
+LOOKALLAROUND
+NOTAUTOAIMED
+MISSILE
+NOBLOCKMAP +MOVEWITHSECTOR
+NOGRAVITY
+DROPOFF
+NOTELEPORT
+FORCEXYBILLBOARD
//+NOTDMATCH
+GHOST
+THRUACTORS
+FLOORCLIP
renderstyle Translucent
alpha 1.0
radius 1
height 1
mass 1
damage 0
}

Actor DummyChecker : Inventory
{
+UNDROPPABLE +UNTOSSABLE
-COUNTITEM
-INVBAR
+PERSISTENTPOWER
inventory.amount 1
inventory.maxamount 9999
}

Actor WallPart : DebrisGeneral
{
bouncetype "Doom"
-NOGRAVITY
+DONTSPLASH
speed 8
bouncefactor 0.4
Renderstyle Translucent
alpha 1.0
scale 0.1
translation "0:4=5:8","5:8=5:8","9:12=5:8","13:15=5:8","16:47=5:8","48:79=5:8","80:111=5:8","112:127=5:8","128:151=5:8","152:159=5:8","160:167=5:8","168:191=5:8","192:207=5:8","208:223=5:8","224:231=5:8","232:235=5:8","236:239=5:8","240:247=5:8","248:249=5:8","250:254=5:8","255:255=5:8"
states
	{
	Spawn:
		BPRT ACDEFGH 2 A_JumpIf(velz == 0,"Null")
		BPRT ACDEFGH 3 A_JumpIf(velz == 0,"Null")
		stop
	 Death:
	 	stop
	 }
}

Actor Smoke_General : DebrisGeneral
{
+NOINTERACTION
vspeed 1
renderstyle Add
alpha 0.3
scale 0.1
states
	{
	Spawn:
		TNT1 A 0 A_SetScale(scalex*randompick(-1,1),scaley*randompick(-1,1))
		SMOK ABCDEFGHIJKLMNOPQ 1 A_SpawnItemEx("ShotSmokeTrail",random(-2,2),random(-2,2),random(-2,2),0,0,0,0) //A_FadeOut(0.01)
		stop
	}
}

Actor BasicSmokeTrail : Smoke_General
{
vspeed 0
alpha 0.15
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(scalex*randompick(-1,1),scaley*randompick(-1,1))
		SMOK CDEFGHIJKLMNOPQ 1
		stop
	}
}

Actor Flare_General : DebrisGeneral
{
+NOINTERACTION +NOCLIP
-MISSILE
-FORCEXYBILLBOARD
renderstyle Add
alpha 0.4
scale 0.4
}


actor ShotSmoke : Smoke_General
{
alpha 0.4
scale 0.18
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(scalex*randompick(-1,1),scaley*randompick(-1,1))
		SMK1 ABCDEFGHIJKLMNOPQR 1 A_SpawnItemEx("ShotSmokeTrail",random(-2,2),random(-2,2),random(-2,2),0,0,0,0)
		stop
	}
}

actor WeaponSmoke : Smoke_General
{
alpha 0.25
scale 0.15
states
	{
	Spawn:
		TNT1 A 0 A_SetScale(scalex*randompick(-1,1),scaley*randompick(-1,1))
		SMK1 ABCDEFGHIJKLMNOPQR 1 A_SpawnItemEx("WeaponSmokeTrail",random(-2,2),random(-2,2),random(-2,2),0,0,0,0)
		stop
	}
}

Actor ShotSmokeTrail : ShotSmoke
{
alpha .1
vspeed 0
scale .16
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(scalex*randompick(-1,1),scaley*randompick(-1,1))
		SMK1 DEFGHIJKLMNOPQR 1
		stop
	}
}

Actor WeaponSmokeTrail : ShotSmoke
{
alpha .1
vspeed 0
scale .12
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(scalex*randompick(-1,1),scaley*randompick(-1,1))
		SMK1 DEFGHIJKLMNOPQR 1
		stop
	}
}

actor SawSmoke : Smoke_General
{
vspeed 0
alpha 0.3
scale 0.22
states
	{
	Spawn:
		TNT1 A 0
		SMK1 AABBCCDDEEFFGGHHIIJJKKLLMMNNOOPPQQRR 1 A_FadeOut(0.01)
		stop
	}
}

Actor RicochetSpark : DebrisGeneral
{
bouncetype None
-NOGRAVITY
+DONTSPLASH
renderstyle ADD
alpha 1.0
radius 3
height 3
scale .03
gravity 1.0
states
	{
	Spawn:
		SPRK A 1 bright A_FadeOut(0.05)
		loop
	}
}

Actor RicochetBullet //: FastProjectile
{
-ACTIVATEIMPACT
-ACTIVATEPCROSS
+FORCEXYBILLBOARD
+THRUACTORS
bouncetype "Hexen"
mass 1
damage 0
radius 1
height 1
speed 30
seesound ""
renderstyle Add
alpha 0.8
//MissileType BulletTrail
//MissileHeight 8
states
	{
	Spawn:
		TNT1 AAA 0 A_CustomMissile ("SparkXNoModel", 2, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_PlaySound("weapons/ricochet")
		PUFF A 2
		stop
	}
}

Actor RicochetShell : RicochetBullet
{
	states
	{
	Spawn:
		TNT1 A 0 A_Jump(100, 6)
		TNT1 AA 0 A_CustomMissile ("ShotgunParticles", 2, 0, random (0, 360), 2, random (0, 360))
		TNT1 AA 0 A_CustomMissile ("ShotgunParticles2", 2, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_PlaySound("weapons/ricochet")
		PUFF A 2
		stop
	}
}

Actor BulletTracer : FastProjectile
{
-ACTIVATEIMPACT
-ACTIVATEPCROSS
+NOTELEPORT
+BLOODLESSIMPACT
+THRUGHOST
alpha .75
renderstyle add
damage 0
speed 100
radius 4
height 4
seesound ""
deathsound ""
states
	{
	Spawn:
		PUFF A 1 bright A_JumpIfCloser(192,2)
		TNT1 A 0 A_FadeOut(0.1)
		loop
		PUFF A 30 bright A_PlaySound("weapons/tracerwhizz",1,1.0,0,8)
		loop
	XDeath:
		TNT1 A 0
		stop
	Death:
		TNT1 A 0 A_Jump(210,"Null")
		TNT1 A 0 A_SpawnProjectile("RicochetBullet",0,0,random(0,360),2,random(-40,40))
		stop
	}
}

Actor EnBulletPuff //replaces BulletPuff //can't replace directly because BulletPuff is internally programmed to spawn behind Revenant tracer
{
+NOGRAVITY
+FORCEXYBILLBOARD
-DONTSPLASH
-ALLOWPARTICLES
decal bulletchip
renderstyle Add
scale 0.1
alpha 0.8
mass 1
states
	{
	Spawn:
		TNT1 AAAAA 0 A_SpawnItemEx("Wallpart",0,0,0,	random(1,5),0,random(2,6),	random(0,360),0,64)
		TNT1 A 0 A_Jump(160, 4)
		TNT1 AAA 0 A_CustomMissile ("SparkXHeavy", 2, 0, random (0, 360), 2, random (0, 180))
		TNT1 A 0 A_Jump(200, 4)
		TNT1 AAA 0 A_CustomMissile ("SparkXHeavy", 2, 0, random (0, 180), 2, random (0, 180))
		TNT1 A 0 A_JumpIf(GetCVAR("evw_tracers")==false,"Puff") // if tracers = ON, they spawn ricochets
		TNT1 A 0 A_Jump(210,"Puff")
		TNT1 A 0 A_SpawnProjectile("RicochetBullet",0,0,random(0,360),2,random(-40,40))
	Puff:		
		TNT1 A 0 A_SpawnItem("ShotSmoke")
		BPUF ABCD 1 bright
		stop
	}
}

Actor ChainsawPuff : EnBulletPuff
{
Damagetype Saw
scale 0.15
alpha 0.6
decal sawmark
states
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAA 0 A_SpawnItemEx("Wallpart",0,0,0,	random(6,9),0,random(6,15),	random(0,360),0,64)
		TNT1 A 0 A_SpawnItem("ShotSmoke")
		TNT1 A 0 A_Jump(180,"Puff")
		TNT1 A 0 A_PlaySound("weapons/chainsaw/hitwall")
		TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("RicochetSpark",0,0,0,	random(1,2),0,random(5,10),random(0,360),0,40)
	Puff:
		CPUF ABCD 1 bright
		TNT1 A 0
		stop
	}
}

Actor EnShotPuff : EnBulletPuff
{
	decal ShotChip
	states
	{
	Spawn:
		TNT1 AAAAA 0 A_SpawnItemEx("Wallpart",0,0,0,	random(1,5),0,random(2,6),	random(0,360),0,64)
		TNT1 A 0 A_Jump(150, 5)
		TNT1 AAAA 0 A_CustomMissile ("ShotgunParticlesHeavy", 2, 0, random (0, 360), 2, random (0, 180))
		TNT1 A 0 A_Jump(180, 5)
		TNT1 AAAA 0 A_CustomMissile ("ShotgunParticlesHeavy", 2, 0, random (0, 180), 2, random (0, 180))
		TNT1 A 0 A_JumpIf(GetCVAR("evw_tracers")==false,"Puff") // if tracers = ON, they spawn ricochets
		TNT1 A 0 A_Jump(210,"Puff")
		TNT1 A 0 A_SpawnProjectile("RicochetShell",0,0,random(0,360),2,random(-40,40))
	}
}

Actor GunBarrelSmoke: ExplosionParticle
{
	Radius 1
	Height 1
	Speed 0
	Damage 0
	Projectile
	SeeSound ""
	DeathSound ""
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("WeaponSmoke",random(4,6),0,random(1,2),0,0,1,0)
		Stop
	Death:
		TNT1 A 0
		Stop
	}
}

Actor ChainSawSmoke: GunBarrelSmoke
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("SawSmoke",random(5,10),random(4,6),random(1,2),0,0,1,0,0,80)
		TNT1 A 0 A_SpawnItemEx("SawSmoke",random(5,10),random(4,6),random(1,2),0,0,1,0,0,210)
		Stop
	}
}

Actor ShotgunBarrelSmoke: GunBarrelSmoke
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("ShotSmoke",random(14,16),random(-1,1),random(1,2),0,0,1,0)
		Stop
	}
}

Actor ShotgunBarrel1Smoke: GunBarrelSmoke
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("ShotSmoke",random(6,8),random(1,2),random(1,2),0,0,1,0)
		Stop
	}
}

Actor ShotgunBarrel2Smoke: GunBarrelSmoke
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("ShotSmoke",random(6,8),random(-1,-2),random(1,2),0,0,1,0)
		Stop
	}
}

Actor ChaingunBarrelSmoke: GunBarrelSmoke
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("WeaponSmoke",random(24,28),random(-6,6),random(1,3),0,0,1,0)
		Stop
	}
}

Actor RocketBarrelSmoke: GunBarrelSmoke
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("WeaponSmoke",random(18,22),random(1,2),random(1,4),0,0,1,0)
		TNT1 A 0 A_SpawnItemEx("WeaponSmoke",random(18,22),random(-1,-2),random(1,4),0,0,1,0)
		Stop
	}
}

Actor RocketBarrelSmoke2: GunBarrelSmoke
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AA 0 A_SpawnItemEx("WeaponSmoke",random(18,22),random(1,3),random(1,4),0,0,1,0)
		TNT1 AA 0 A_SpawnItemEx("WeaponSmoke",random(18,22),random(-1,-3),random(1,4),0,0,1,0)
		Stop
	}
}

Actor PlasmaBarrelSmoke: GunBarrelSmoke
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAA 1 A_SpawnItemEx("WeaponSmoke",random(20,24),random(-2,4),random(2,5),0,0,1,0)
		Stop
	}
}

Actor PlasmaBarrelSmoke2: GunBarrelSmoke
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAAAAAAAAAAAA 1 A_SpawnItemEx("WeaponSmoke",random(5,15),random(-3,3),random(-2,2),0,0,1,0)
		TNT1 AAAAAAAA 1 A_SpawnItemEx("WeaponSmoke",random(10,20),random(-2,2),random(-2,2),0,0,1,0)
		TNT1 AAA 1 A_SpawnItemEx("WeaponSmoke",random(20,25),random(-1,1),random(-2,2),0,0,1,0)
		Stop
	}
}

Actor BigFuckingBarrelSmoke: GunBarrelSmoke
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAAAAAAAAAAAA 1 A_SpawnItemEx("WeaponSmoke",random(0,12),random(-6,6),random(-2,2),0,0,1,0)
		TNT1 AAAAAAAA 1 A_SpawnItemEx("WeaponSmoke",random(10,22),random(-5,5),random(-2,2),0,0,1,0)
		TNT1 AAA 1 A_SpawnItemEx("WeaponSmoke",random(25,30),random(-1,1),random(-2,2),0,0,1,0)
		Stop
	}
}

Actor RocketFlare : Flare_General
{
renderstyle Add
alpha .4
scale .09
states
	{
	Spawn:
		TNT1 A 0 A_Warp(AAPTR_MASTER,-16,0,6)
		LENR A 1 bright
		wait
	}
}

Actor Rocket1Trail : Smoke_General
{
scale 0.1
renderstyle translucent
alpha 0.3
states
	{
	Spawn:
		TNT1 A 0 A_SetScale(scalex*randompick(-1,1),scaley*randompick(-1,1))
		SMOK ABCDEFGHIJKLMNOPQ 1 A_FadeOut(0.01)
		stop
	}
}

Actor BD_RocketTrail : Rocket1Trail
{
alpha 0.22
}

Actor HomingRocketFlare : Flare_General
{
renderstyle Add
alpha .4
scale .09
states
	{
	Spawn:
		TNT1 A 0 A_Warp(AAPTR_MASTER,-16,0,6)
		TNT1 A 0 A_JumpIfMasterCloser(18,"Spawn")
		stop
	}
}

Actor BD_SeekerRocketTrail : BD_RocketTrail
{
scale 0.07
}

Actor SeekerFlare : Flare_General
{
+FORCEXYBILLBOARD
+NOGRAVITY
alpha .5
scale .12
states
	{
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound("weapons/rlan/seek",0)
		LENG A 2
		stop
	}
}
 
Actor SmokingPiece : DebrisGeneral
{
-NOGRAVITY
gravity 0.7
alpha 1.0
states
	{
	Spawn:
		TNT1 A 0 A_FadeOut(0.025)
		TNT1 A 1 A_SpawnItem("SmokingPieceSmoke")
		loop
	Death:
		TNT1 A 0
		stop
	}
}

Actor SmokingPieceSmoke : BasicSmokeTrail
{
scale 0.04
alpha 0.3
}


Actor BlueFlarePlasma : Flare_General
{
renderstyle Add
alpha .5
scale .1
states
	{
	Spawn:
		TNT1 A 0 A_Warp(AAPTR_MASTER)
	Spawn1:
		TNT1 A 0 A_JumpIfMasterCloser(1,"Spawn1")
		LENB A 1 bright A_Fadeout(0.1)
		wait
	}
}

Actor BlueFlarePlasmaTrail : Flare_General //appears behind the plasmaball as a fading out trail
{
+FORCEXYBILLBOARD
alpha .4
scale .07
states
	{
	Spawn:
		LENB A 1 bright A_FadeOut(0.15)
		TNT1 A 0 A_SetScale(scalex*0.9,scaley*0.9)
		loop
	}
}

Actor BluePlasmaPiece : DebrisGeneral
{
+DONTSPLASH
-NOGRAVITY
renderstyle Add
alpha 1.0
scale 0.08
speed 6
gravity 0.8
states
	{
	Spawn:
		TNT1 A 0
		PLBS A 1 bright A_FadeOut(0.03)
		loop
	}
}

Actor PlasmaRailBall : DebrisGeneral
{
-MISSILE
+NOINTERACTION
renderstyle Add
alpha .8
scale .03
translation "9:12=242:245", "112:127=192:207","152:159=200:207"
states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItem("PlamaRailFlare")
		PEXP ABCDEF 1 bright A_FadeOut(0.0025)
		PEXP GHIJKLM 2 bright A_FadeOut(0.0025)
		stop
	}
}

Actor BluePlasmaShred : DebrisGeneral
{
+NOINTERACTION
renderstyle ADD
speed 5
scale .05
alpha .6
states
{
	Spawn:
		PLSE ABCDE 1 bright A_SpawnItem("BluePlasmaShredTrail")
		stop
	}
}

Actor BluePlasmaShredTrail : BluePlasmaShred
{
speed 0
alpha .5
states
{
	Spawn:
		PLSE ABCDE 2 bright
		stop
	}
}

Actor PlamaRailFlareCounter : DummyChecker
{
inventory.maxamount 500
}

Actor PlamaRailFlare : BlueFlarePlasmaTrail
{
alpha .1
scale .07
states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_GiveInventory("PlamaRailFlareCounter",1,AAPTR_PLAYER1)
		TNT1 A 0 A_JumpIfInventory("PlamaRailFlareCounter",250,"Disappear",AAPTR_PLAYER1)
		TNT1 A 1
		TNT1 A 0 A_TakeInventory("PlamaRailFlareCounter",1,0,AAPTR_PLAYER1)
		LENB A 1 bright A_FadeOut(0.0025)
		wait
	Disappear:
		TNT1 A 1 A_TakeInventory("PlamaRailFlareCounter",1,0,AAPTR_PLAYER1)
		stop
	}
}

Actor BFGTrail : Flare_General
{
alpha 0.4
scale .5
+NOINTERACTION
states
	{	
	Spawn:
		TNT1 A 0 A_Warp(AAPTR_MASTER)
		LENG A 1 bright
		wait
	}
}

Actor BFGBallRay : PlasmaRailBall
{
alpha .5
translation "192:207=112:127"
states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItem("BFGBallRayFlare")
		PEXP ABCDEFGHIJKLM 1 bright A_FadeOut(0.08)
		stop
	}
}

Actor BFGBallRayFlare : Flare_General
{
renderstyle Add
alpha .1
scale .07
states
	{
	Spawn:
		LENG A 1 bright A_FadeOut(0.01)
		wait
	}
}

Actor BFGBallRayPuff
{
damagetype none
+BLOODLESSIMPACT
}

Actor BD_BFGExtra : BFGExtra
{
translation "192:207=112:127","240:247=124:127"
damagetype BFGSplash
scale .35
renderstyle Add
alpha .6
states
	{
	Spawn:
		TNT1 AAAAAAAAAA 0 A_SpawnProjectile("BFGGreenPlasmaPiece",0,0,random(-360,360),2,random(-80,80))
		PEXP AABBCCDDEEFFGGHHIIJJKKLLMM 1 bright
		stop
	}
}

Actor BFGGreenPlasmaPiece : BluePlasmaPiece
{
translation "192:207=112:127","240:247=9:12"
+NOINTERACTION
+NOGRAVITY
alpha .5
speed 3
scale .1
}

Actor BFGGreenPlasmaShred : BluePlasmaShred
{
translation "192:207=112:127","240:247=9:12"
states
	{
	Spawn:
		PLSE ABCDE 1 bright A_SpawnItem("BFGGreenPlasmaShredTrail")
		stop
	}
}

Actor BFGGreenPlasmaShredTrail : BluePlasmaShredTrail
{
translation "192:207=112:127","240:247=9:12"
}

Actor BFGRailBall : Flare_General
{
renderstyle Add
alpha .6
scale .17
translation "192:207=112:127"
states
	{
	Spawn:

		TNT1 A 0
		TNT1 A 0 A_SpawnItem("BFGRailFlare")
		PEXP ABCDEFGHIJKLM 1 bright A_FadeOut(0.1)
		stop
	}
}

Actor BFGRailPuff : BD_BFGExtra
{
+BLOODLESSIMPACT
+ALWAYSPUFF
damagetype BFGSplash
scale .35
renderstyle Add
alpha .6
states
	{
	Spawn:
		TNT1 AAAAAAAAAAAA 0 A_SpawnProjectile("BFGGreenPlasmaPiece",0,0,random(-360,360),2,random(-50,50))
		TNT1 AAAAAA 0 A_SpawnProjectile("BFGGreenPlasmaShred",0,0,random(-360,360),2,random(-90,90))
		TNT1 A 3
		stop
	}
}