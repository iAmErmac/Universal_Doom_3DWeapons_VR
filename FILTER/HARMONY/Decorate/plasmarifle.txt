ACTOR Vanilla_PlasmaRifle : PlasmaRifle
{
	Weapon.AmmoType2 "Cell"
	Weapon.AmmoUse2 1
	States
	{
	Deselect:
		TNT1 A 0 A_JumpIf(GetCVAR("evw_fastswitch")==0, 2)
		PLSG A 1 A_Lower(60)
		Loop
		PLSG A 1 A_Lower
		Loop
	Select:
		TNT1 A 0 A_JumpIf(GetCVAR("evw_fastswitch")==0, 2)
		PLSG A 1 A_Lower(60)
		Loop
		PLSG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIf(GetCVAR("evw_weapons")==1, "Fire.Enhanced")
		PLSG A 3 A_FirePlasma
		PLSG B 20 A_ReFire
		Goto Ready
	Fire.Enhanced:
		TNT1 A 0 A_CheckReload
		TNT1 A 0 A_GunFlash
		PLSG A 3 bright A_Firecustommissile("BD_Plasmaball",0,1)
		PLSG B 20 A_ReFire
		Goto Ready
	Altfire:
		TNT1 A 0 A_JumpIf(GetCVAR("evw_weapons")==0, "Ready") //Alt fire is available on enhanced mode only
		PLSG A 6 A_PlaySound("weapons/plasma/charge")
	Altfire.Enhanced:
		TNT1 A 0 A_CheckReload
		// TNT1 A 0 A_GunFlash
		PLSG A 1 A_Firecustommissile ("PlasmaBeam", 0, 1)
		PLSG A 1 A_ReFire ("Altfire.Enhanced")
		//PLSG AAA 1 A_SpawnItemEx("WeaponSmoke",cos(pitch)*25,random(0,-6),sin(-pitch)*25+random(24,36),0,0,1,0)
		TNT1 A 0 A_FireProjectile("PlasmaBarrelSmoke",0,0,0,0,FPF_NOAUTOAIM,0)
		TNT1 B 0 A_PlaysoundEx("weapons/plasma/cool", soundslot7, 1)
		//PLSG AAAAAAAAAAAAAAA 1 A_SpawnItemEx("WeaponSmoke",cos(pitch)*25,random(-8,-16),sin(-pitch)*25+random(32,40),0,0,1,0)
		//PLSG AAAAAAAA 1 A_SpawnItemEx("WeaponSmoke",cos(pitch)*25,random(0,-10),sin(-pitch)*25+random(24,36),0,0,1,0)
		//PLSG AAA 1 A_SpawnItemEx("WeaponSmoke",cos(pitch)*25,random(3,-3),sin(-pitch)*25+random(24,36),0,0,1,0)
		TNT1 A 0 A_FireProjectile("PlasmaBarrelSmoke2",0,0,0,0,FPF_NOAUTOAIM,0)
		PLSG A 10 A_StopSoundEx (soundslot7)
		goto Ready
	Spawn:
		TNT1 A 0 A_JumpIf(GetCVAR("evw_pickupmodel")==1, "Spawn.Model")
		PLAS A 1
		Loop
	Spawn.Model:
		MPLS A 1
		Goto Spawn
	}
}

Actor BD_Plasmaball : FastProjectile
{  
Radius 13
Height 8
Speed 25
Damage 5
Obituary "$OB_MPPLASMARIFLE" // "%o was melted by %k's plasma gun."
missiletype "BlueFlarePlasmaTrail"
missileheight 8
+DONTBLAST
damagetype PlasmaDamage
+FORCEXYBILLBOARD
+THRUGHOST
+NOEXTREMEDEATH
+BLOODLESSIMPACT
decal BluePlasmaSplat
renderstyle ADD
scale .2
alpha .8
SeeSound ""
DeathSound "weapons/plasmax"
States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/plasma/fire")
		TNT1 A 0 A_SetScale(scalex*randompick(-1,1),scaley*randompick(-1,1))
		TNT1 A 0 A_SpawnItemEx("BlueFlarePlasma",0,0,0,velx,vely,velz,0,SXF_SETMASTER|SXF_ORIGINATOR|SXF_ABSOLUTEVELOCITY)
		PLBA ABCDEF 2 bright
		goto spawn+4
	Death:
		TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING)
		TNT1 AAAAAAAAAAAA 1 A_SpawnProjectile("BluePlasmaPiece",0,0,random(-360,360),2,random(-50,50))
		TNT1 AAAAAA 0 A_SpawnProjectile("BluePlasmaShred",0,0,random(-360,360),2,random(-90,90))
		TNT1 A 0 bright A_SpawnItem("WeaponSmoke")
	Explosion:
		TNT1 A 0 
		TNT1 A 0 A_SetScale(0.2*randompick(-1,1),0.2*randompick(-1,1))
		PEXP ABCDEFGHIJKLM 2 bright A_FadeOut(0.05)
		stop
	}
}

ACTOR PlasmaBeam : BD_Plasmaball
{
	Speed 60
	Radius 13
	Height 8
	Damage 7
	Decal Scorch
	damagetype "PlasmaDamage"
	-RIPPER
	-CANNOTPUSH
	-NODAMAGETHRUST
	-NOEXTREMEDEATH
	+SPAWNSOUNDSOURCE
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/plasma/fire")
		TNT1 A 0 A_SetScale(scalex*randompick(-1,1),scaley*randompick(-1,1))
		TNT1 AAAA 3 A_SpawnItemEx("BlueFlarePlasma",0,0,0,velx,vely,velz,0,SXF_SETMASTER|SXF_ORIGINATOR|SXF_ABSOLUTEVELOCITY)
		PLBA ABCDEF 2 bright
		goto spawn+4
	Death:
	Explosion:
		TNT1 A 0 
		stop
	}
}